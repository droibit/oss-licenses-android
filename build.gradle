buildscript {
  dependencies {
    classpath libs.plugin.ossLicense
  }
}

plugins {
  alias(libs.plugins.android.application) apply false
  alias(libs.plugins.android.library) apply false
  alias(libs.plugins.kotlin) apply false
  alias(libs.plugins.spotless) apply false
}

ext {
  compileSdk = 33
  targetSdk = 33
}

subprojects {
  apply plugin: "com.diffplug.spotless"

  plugins.whenPluginAdded { plugin ->
    if (plugin instanceof com.android.build.gradle.BasePlugin) {
      android {
        compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
      }

      spotless {
        kotlin {
          target("**/*.kt")
          targetExclude("$buildDir/**/*.kt")
          targetExclude("**/generated/**/*.kt")
          // Spotless doesn't read .editorconfig yet: https://github.com/diffplug/spotless/issues/142
          ktlint(libs.versions.ktlint.get()).editorConfigOverride([
            'disabled_rules'                             : 'package-name',
            'insert_final_newline'                       : 'true',
            'end_of_line'                                : 'lf',
            'charset'                                    : 'utf-8',
            'indent_size'                                : '2',
            'ij_kotlin_allow_trailing_comma'             : 'true',
            'ij_kotlin_allow_trailing_comma_on_call_site': 'true',
            'ij_kotlin_imports_layout'                   : '*',
          ])
        }

        format('xml') {
          target("**/*.xml")
          targetExclude("**/build/**/*.xml")

          trimTrailingWhitespace()
          indentWithSpaces(2)
          endWithNewline()
        }
      }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
      task.kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
          '-opt-in=androidx.compose.material3.ExperimentalMaterial3Api',
        ]
      }
    }
  }
}
