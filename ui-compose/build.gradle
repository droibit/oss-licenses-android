plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'maven-publish'
}

android {
  namespace 'com.github.droibit.oss_licenses.ui.compose'
  compileSdk rootProject.ext.compileSdk

  defaultConfig {
    minSdk 24
    targetSdkVersion rootProject.ext.targetSdk

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  buildFeatures {
    compose true
    buildConfig false
  }

  composeOptions {
    kotlinCompilerExtensionVersion libs.versions.androidx.compose.compiler.get()
  }

  packagingOptions {
    exclude 'META-INF/core_debug.kotlin_module'
  }
}

dependencies {
  api project(":parser")

  implementation libs.androidx.activity.compose
  implementation libs.androidx.navigation.compose
  implementation libs.bundles.androidx.compose.ui.material3
  debugImplementation libs.androidx.compose.ui.tooling
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release

        pom {
          licenses {
            license {
              name = "The Apache License, Version 2.0"
              url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
        }
      }
    }
  }
}
