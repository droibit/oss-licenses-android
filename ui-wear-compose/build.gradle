plugins {
  id "com.android.library"
  id "org.jetbrains.kotlin.android"
  id "maven-publish"
}

android {
  namespace "com.github.droibit.oss_licenses.ui.wear.compose"
  compileSdk libs.versions.compileSdk.get().toInteger()

  defaultConfig {
    minSdk 25

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
  }

  buildFeatures {
    compose true
  }

  composeOptions {
    kotlinCompilerExtensionVersion libs.versions.androidx.compose.compiler.get()
  }

  kotlinOptions {
    freeCompilerArgs += [
      "-opt-in=androidx.wear.compose.material.ExperimentalWearMaterialApi",
      "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi",
    ]
  }

  packagingOptions {
    resources {
      excludes += [
        "/META-INF/{AL2.0,LGPL2.1}",
        "/META-INF/core_debug.kotlin_module"
      ]
    }
  }
}

dependencies {
  api project(":ui-viewmodel")

  implementation libs.androidx.activity.compose
  implementation libs.androidx.navigation.compose
  implementation libs.bundles.androidx.wear.compose
  implementation libs.androidx.wear.compose.navigation
  implementation libs.androidx.compose.ui.tooling
  debugImplementation libs.androidx.compose.ui.tooling.preview
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release

        pom {
          licenses {
            license {
              name = "The Apache License, Version 2.0"
              url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
        }
      }
    }
  }
}

